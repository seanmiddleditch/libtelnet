cmake_minimum_required(VERSION 3.5)
project(libtelnet
    VERSION 0.30.0
    LANGUAGES C
)
set(PROJECT_DESCRIPTION "TELNET protocol handling library")
set(PROJECT_HOMEPAGE_URL https://github.com/seanmiddleditch/libtelne)

if (MSVC)
    add_definitions(-D_SCL_SECURE_NO_WARNINGS -D_CRT_SECURE_NO_WARNINGS)
    if (LIBTELNET_STRICT)
        add_compile_options(/WX)
    endif ()
else ()
    if (LIBTELNET_STRICT)
        add_compile_options(-Wall -Wextra -pedantic -Werror)
    endif ()
endif ()

add_library(libtelnet libtelnet.c)

target_include_directories(libtelnet
    PUBLIC
        $<INSTALL_INTERFACE:include>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}
)

add_subdirectory(util)
add_subdirectory(doc)
add_subdirectory(test)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

configure_file(libtelnet.pc.in libtelnet.pc)

install(TARGETS libtelnet
    EXPORT libtelnetConfig
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)
install(FILES libtelnet.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
install(EXPORT libtelnetConfig
    FILE libtelnetConfig.cmake
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/libtelnet
)
write_basic_package_version_file(
    "${CMAKE_BINARY_DIR}/libtelnetConfigVersion.cmake"
    COMPATIBILITY AnyNewerVersion)
install(FILES "${CMAKE_BINARY_DIR}/libtelnetConfigVersion.cmake"
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/libtelnet")

include(CPack)
